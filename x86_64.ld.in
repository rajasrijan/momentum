#include "kernel/config.h"

ENTRY(loader)
TARGET(elf64-x86-64)

SECTIONS
{
	. = 0x100000;
	kernel_start = . + KERNEL_BASE_PTR;
	.text0 ALIGN (0x1000) : AT(ADDR(.text0))
	{
		*(.multiboot)
		*(.text0*)
		. = ALIGN(4096);
	}

	.data0 ALIGN (0x1000) : AT(ADDR(.data0))
	{
		*(.data0*)
		. = ALIGN(4096);
	}

	. += KERNEL_BASE_PTR;
	.text ALIGN (0x1000) : AT(ADDR(.text) - KERNEL_BASE_PTR)
	{
		*(.text*)
		*(.rodata*)
		. = ALIGN(4096);
	}
	
	.data ALIGN (0x1000) : AT(ADDR(.data) - KERNEL_BASE_PTR)
	{
		*(.data)
        . = ALIGN(4096);
	}
	
	__MOD_LIST__ = .;
    .mod_init ALIGN (0x1000) : AT(ADDR(.mod_init) - KERNEL_BASE_PTR)
	{
		*(.mod_init)
	}
	__MOD_END__ = .;
    
	.ctors ALIGN (0x1000) : AT(ADDR(.ctors) - KERNEL_BASE_PTR)
	{
		__CTOR_LIST__ = .;
		*(.ctors)
		*(.init_array)
	}
	__CTOR_END__ = .;
    
	.dtors ALIGN (0x1000) : AT(ADDR(.dtors) - KERNEL_BASE_PTR)
	{
		*(.dtors)
		*(.fini_array)
	}
	
	.bss ALIGN (0x1000) : AT(ADDR(.bss) - KERNEL_BASE_PTR)
	{
		_sbss = .;
		*(COMMON)
		*(.bss)
		_ebss = .;
		. = ALIGN(4096);
	}
	
	.eh_frame ALIGN (0x1000) : AT(ADDR(.eh_frame) - KERNEL_BASE_PTR)
	{
		*(.eh_frame)
		. = ALIGN(4096);
	}
	kernel_end = .;
}
